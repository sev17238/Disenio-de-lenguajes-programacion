Cocol =
   { ANY }            /* Any global python code */
   "COMPILER" ident
   { ANY }            /* Parser class member fields and methods */
   ScannerSpecification
   ParserSpecification
   "END" ident '.'.
ScannerSpecification =
   ["IGNORECASE"]
   ["CHARACTERS" {SetDecl}]
   ["TOKENS" {TokenDecl}]
   ["NAMES" {NameDecl}]
   ["PRAGMAS" {PragmaDecl}]
   {CommentDecl}
   {WhiteSpaceDecl}.
SetDecl = ident '=' Set.
Set = BasicSet { ('+'|'-') BasicSet }.
BasicSet = string | ident | Char [".." Char] | "ANY".
Char = char | "CHR" '(' number ')'.
TokenDecl = Symbol ['=' TokenExpr '.'].
TokenExpr = TokenTerm {'|' TokenTerm}.
TokenTerm = TokenFactor {TokenFactor} ["CONTEXT" '(' TokenExpr ')'].
TokenFactor = Symbol
   | '(' TokenExpr ')'
   | '[' TokenExpr ']'
   | '{' TokenExpr '}'.
Symbol = ident | string | char.
NameDecl = ident "=" ( ident | string ) "." .
PragmaDecl = TokenDecl [SemAction].
CommentDecl = "COMMENTS" "FROM" TokenExpr "TO" TokenExpr [ "NESTED" ].
WhiteSpaceDecl = "IGNORE" Set.
ParserSpecification = "PRODUCTIONS" {Production}.
Production = ident [Attributes] [SemAction] '=' Expression '.'.
Expression = Term {'|' Term}.
Term = [[Resolver] Factor {Factor}].
Factor = ["WEAK"] Symbol [Attributes]
   | '(' Expression ')'
   | '[' Expression ']'
   | '{' Expression '}'
   | "ANY"
   | "SYNC"
   | SemAction.
Attributes = '<' {ANY} '>' | '<.' {ANY} '.>'.
SemAction = "(." {ANY} ".)".
Resolver = "IF" '(' {ANY} ')'.